##### restrict-ingress-sg-rule-cidr-blocks.sentinel #####
# This policy uses the Sentinel tfplan import to validate that
# no security group rules have the CIDR "0.0.0.0/0"

##### Imports #####

import "tfplan"
import "strings"
import "types"

##### Functions #####

# Find all resources of a specific type
# from all modules using the tfplan import
find_resources_from_plan = func(type) {

  resource_maps = {}

  # Iterate over all modules in the tfplan import
  for tfplan.module_paths as path {

    # Compute joined_path from the module path
    if length(path) == 0 {
      joined_path = ""
    } else {
      joined_path = "module." + strings.join(path, ".module.") + "."
    }

    # Append all resources of the specified type to resource_maps
    # setting the key to joined_path. Append the empty map, {}, if the
    # module does not have any resources of the specified type.
    resource_maps[joined_path] = tfplan.module(path).resources[type] else {}
  }

  return resource_maps
}

# Get the full address of a resource instance including modules, type,
# name, and index in form module.<A>.module.<B>.<type>.<name>[<index>]
# module_path is like module paths returned by tfplan.module_paths
get_instance_address = func(joined_path, type, name, index) {
  address = joined_path + type + "." + name + "[" + string(index) + "]"
  return address
}

# Validate that all AWS ingress security group rules
# do not have cidr_block 0.0.0.0/0
validate_sgr_cidr_blocks = func() {

  # Initialize validated to true
  # This will be set to false if any SG rules violate rule
  validated = true

  # Set resource_type
  resource_type = "aws_security_group_rule"

  # Get all resources of specified type
  resource_maps = find_resources_from_plan(resource_type)

  # Loop through the module-level resource maps
  for resource_maps as module_path, resource_map {
    # Loop through the named resources
    for resource_map as name, instances {
      # Loop through resource instances
      for instances as index, r {

        # Get addresses of the resource and the instance
        address = get_instance_address(module_path, resource_type, name, index)

        # Skip resources that are being destroyed
        # to avoid unnecessary policy violations
        if length(r.diff) == 0 {
          print("Skipping resource", address,
            "that is being destroyed.")
          continue
        }

        # Determine if the attribute is computed
        if r.diff["type"].computed else false or
          r.diff["cidr_blocks.#"].computed else false is true {
          # Print message indicating a computed value was found
          print("Security group rule", address,
            "has attributes, type and/or cidr_blocks that are computed.")
          # If you want computed values to cause the policy to fail, uncomment the next line.
          #validated = false
        } else {
          # Validate that each SG rule does not have disallowed value
          # If it does, print violation message
          # Since cidr_blocks is optional and could be computed,
          # We check that it is present and really a list
          # before checking whether it contains "0.0.0.0/0"
          if r.applied.type is "ingress" and
             length(r.applied.cidr_blocks) else 0 > 0 and
             types.type_of(r.applied.cidr_blocks) is "list" and
             r.applied.cidr_blocks contains "0.0.0.0/0" {
               print("Security group rule", address, "of type ingress",
                 "contains disallowed cidr_block 0.0.0.0/0" )
               validated = false
          }
        } // end computed check

      } // end resource instances
    } // end named resources
  } // end resorce maps

  # Return validated which will be false if there were any violations
  return validated
}

##### Rules #####

# Call the validation function and assign results
sgrs_validated = validate_sgr_cidr_blocks()

# Main rule
main = rule {
  sgrs_validated is true
}
